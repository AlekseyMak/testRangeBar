apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

version = "1.1.0"
group = "com.almadev"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'Android range bar with thumbs toggle'
            packaging 'aar'
            description 'Android range bar with thumbs toggle with material design'
            url 'https://github.com/VandalSoftware/android-cache-lib'

            scm {
                url 'scm:git@github.com:VandalSoftware/android-cache-lib.git'
                connection 'scm:git@github.com:VandalSoftware/android-cache-lib.git'
                developerConnection 'scm:git@github.com:VandalSoftware/android-cache-lib.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'jle'
                    name 'Jonathan Le'
                    email 'jonathanle@gmail.com'
                }
            }
        }
    }
}